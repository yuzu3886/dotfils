typeset -U path fpath

path=(
  ${HOME}/go/bin(N-/)
  ${path}
)

fpath=(
  ${HOME}/.asdf/completions
  ${fpath}
)

source ${HOME}/.asdf/asdf.sh

autoload -Uz add-zsh-hook

autoload -Uz colors
colors

autoload -Uz compinit
compinit -C -d ${XDG_CACHE_HOME}/zsh/compdump

autoload -Uz history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end  history-search-end

autoload -Uz vcs_info
zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:git:*' check-for-changes true
zstyle ':vcs_info:git:*' stagedstr '+'
zstyle ':vcs_info:git:*' unstagedstr '-'
zstyle ':vcs_info:git:*' formats '%b' '%c%u'
zstyle ':vcs_info:git:*' actionformats '%b' '%c%u' '%a'
_update_vcs_info_msg() {
  local -a messages
  local msg
  LANG=en_US.UTF-8 vcs_info
  if [ -z "${vcs_info_msg_0_}" ]
  then
    msg=''
  else
    messages+=( "%B${vcs_info_msg_0_}%b" )
    [ -n "${vcs_info_msg_1_}" ] && messages+=( "%B%F{yellow}${vcs_info_msg_1_}%f%b" )
    [ -n "${vcs_info_msg_2_}" ] && messages+=( "%B%F{red}!${vcs_info_msg_2_}%f%b" )
    msg="${(j: :)messages}"
  fi
  typeset -g vcs_info_msg="${msg}"
}
add-zsh-hook precmd _update_vcs_info_msg

HISTFILE=${XDG_DATA_HOME}/zsh/history
HISTSIZE=100000
SAVEHIST=100000

setopt hist_ignore_all_dups
setopt hist_ignore_space
setopt hist_reduce_blanks
setopt hist_verify
setopt ignore_eof
setopt no_beep
setopt no_flow_control
setopt prompt_subst
setopt share_history

zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

PROMPT='%~ ${vcs_info_msg:+${vcs_info_msg} }%(?.%F{blue}❯%F{cyan}❯%F{green}❯%f.%F{red}❯❯❯%f) '

fzf-history() {
  local hist
  hist=$(history -n -r 1 | fzf --layout=reverse --no-sort --query="${LBUFFER}")
  if [ -n "${hist}" ]
  then
    BUFFER=${hist}
    CURSOR=${#BUFFER}
  fi
}
zle -N fzf-history

fzf-ghq() {
  local position preview_command repo
  if [ "$(tput cols)" -gt 160 ]
  then
    position='right,border-left'
  else
    position='down,border-top,70%'
  fi
  preview_command="bat --color=always --theme=gruvbox-dark $(ghq root)/{}/README*"
  repo=$(ghq list | fzf --layout=reverse --query="${LBUFFER}" \
    --preview="${preview_command}" --preview-window="${position}" \
    --bind ctrl-d:preview-page-down,ctrl-u:preview-page-up)
  if [ -n "${repo}" ]
  then
    repo=$(ghq list --full-path --exact "${repo}")
    BUFFER="cd ${repo}"
    zle accept-line
  fi
}
zle -N fzf-ghq

bindkey -e

bindkey '^p' history-beginning-search-backward-end
bindkey '^n' history-beginning-search-forward-end

bindkey '^r'   fzf-history
bindkey '^xf'  fzf-ghq
bindkey '^x^f' fzf-ghq
