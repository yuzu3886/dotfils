typeset -U path fpath

path=(
  ${HOME}/go/bin(N-/)
  ${path}
)

fpath=(
  ${HOME}/.asdf/completions
  ${fpath}
)

source ${HOME}/.asdf/asdf.sh

autoload -Uz colors
colors

autoload -Uz compinit
compinit -C -d ${XDG_CACHE_HOME}/zsh/compdump

autoload -Uz history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end  history-search-end

HISTFILE=${XDG_DATA_HOME}/zsh/history
HISTSIZE=100000
SAVEHIST=100000

setopt hist_ignore_all_dups
setopt hist_ignore_space
setopt hist_reduce_blanks
setopt hist_verify
setopt ignore_eof
setopt no_beep
setopt no_flow_control
setopt share_history

zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

PROMPT='%~ %(?.%F{blue}❯%F{cyan}❯%F{green}❯%f.%F{red}❯❯❯%f) '

fzf-history() {
  local hist
  hist=$(history -n -r 1 | fzf --layout=reverse --no-sort --query="${LBUFFER}")
  if [ -n "${hist}" ]
  then
    BUFFER=${hist}
    CURSOR=${#BUFFER}
  fi
}
zle -N fzf-history

fzf-ghq() {
  local preview_command repo
  preview_command="bat --color=always --theme=gruvbox-dark $(ghq root)/{}/README*"
  repo=$(ghq list | fzf --layout=reverse --query=${LBUFFER} --preview="${preview_command}")
  if [ -n "${repo}" ]
  then
    repo=$(ghq list --full-path --exact ${repo})
    BUFFER="cd ${repo}"
    zle accept-line
  fi
}
zle -N fzf-ghq

bindkey -e

bindkey '^p' history-beginning-search-backward-end
bindkey '^n' history-beginning-search-forward-end

bindkey '^r'   fzf-history
bindkey '^xf'  fzf-ghq
bindkey '^x^f' fzf-ghq
